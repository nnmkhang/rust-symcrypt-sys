//
//  fdef_369asm.asm   Assembler code for large integer arithmetic in the default data format
//  Expresses asm in a generic enough way to enable generation of MASM and GAS using the
//  symcryptasm_processor.py script and C preprocessor
//
// This file contains alternative routines that are used for modular computations
// where the modulus is 257-384 or 513-576 bits long.
// (Currently on ARM64 it is also used for 0-192-bit moduli but not on AMD64)
//
// The immediate advantage is that it improves EC performance on 384, and 521-bit curves.
//
// Most of this code is a direct copy of the default code.
// AMD64 digits are now 512 bits.
// We read the 'ndigit' value. If it is 1 digit, the values are 6 64-bit words, if it is 2 the values
// are 9 64-bit words. As we compute in groups of 3, our loop counters are one more than nDigit
//
// Copyright (c) Microsoft Corporation. Licensed under the MIT license.

#include "symcryptasm_shared.cppasm"

// A digit consists of 4 words of 64 bits each

//UINT32
//SYMCRYPT_CALL
// SymCryptFdef369RawAddAsm(
//     _In_reads_bytes_(nDigits * SYMCRYPT_FDEF_DIGIT_SIZE )   PCUINT32    Src1,
//     _In_reads_bytes_(nDigits * SYMCRYPT_FDEF_DIGIT_SIZE )   PCUINT32    Src2,
//     _Out_writes_bytes_(nDigits * SYMCRYPT_FDEF_DIGIT_SIZE ) PUINT32     Dst,
//                                                             UINT32      nDigits )
FUNCTION_START(SymCryptFdef369RawAddAsm, 4, 5)

        inc     D4
        xor     Q0, Q0

SymCryptFdef369RawAddAsmLoop:
        // carry is in the carry flag
        mov     Q0,[Q1]
        adc     Q0,[Q2]
        mov     [Q3],Q0

        mov     Q0,[Q1 + 8]
        adc     Q0,[Q2 + 8]
        mov     [Q3 + 8], Q0

        mov     Q0,[Q1 + 16]
        adc     Q0,[Q2 + 16]
        mov     [Q3 + 16], Q0

        lea     Q1, [Q1 + 24]
        lea     Q2, [Q2 + 24]
        lea     Q3, [Q3 + 24]
        dec     D4
        jnz     SymCryptFdef369RawAddAsmLoop

        mov     Q0, 0
        adc     Q0, Q0

FUNCTION_END(SymCryptFdef369RawAddAsm)

// UINT32
// SYMCRYPT_CALL
// SymCryptFdef369RawSubAsm(
//     _In_reads_bytes_(nDigits * SYMCRYPT_FDEF_DIGIT_SIZE )   PCUINT32    pSrc1,
//     _In_reads_bytes_(nDigits * SYMCRYPT_FDEF_DIGIT_SIZE )   PCUINT32    pSrc2,
//     _Out_writes_bytes_(nDigits * SYMCRYPT_FDEF_DIGIT_SIZE ) PUINT32     pDst,
//                                                             UINT32      nDigits )

FUNCTION_START(SymCryptFdef369RawSubAsm, 4, 5)

        inc     D4
        xor     Q0, Q0

SymCryptFdef369RawSubAsmLoop:
        // carry is in the carry flag
        mov     Q0,[Q1]
        sbb     Q0,[Q2]
        mov     [Q3],Q0

        mov     Q0,[Q1 + 8]
        sbb     Q0,[Q2 + 8]
        mov     [Q3 + 8], Q0

        mov     Q0,[Q1 + 16]
        sbb     Q0,[Q2 + 16]
        mov     [Q3 + 16], Q0

        lea     Q1, [Q1 + 24]
        lea     Q2, [Q2 + 24]
        lea     Q3, [Q3 + 24]
        dec     D4
        jnz     SymCryptFdef369RawSubAsmLoop

        mov     Q0, 0
        adc     Q0, Q0

FUNCTION_END(SymCryptFdef369RawSubAsm)

// VOID
// SYMCRYPT_CALL
// SymCryptFdef369MaskedCopyAsm(
//     _In_reads_bytes_( nDigits*SYMCRYPT_FDEF_DIGIT_SIZE )        PCBYTE      pbSrc,
//     _Inout_updates_bytes_( nDigits*SYMCRYPT_FDEF_DIGIT_SIZE )   PBYTE       pbDst,
//                                                                 UINT32      nDigits,
//                                                                 UINT32      mask )

FUNCTION_START(SymCryptFdef369MaskedCopyAsm, 4, 6)

        inc     D3
        movsxd  Q4, D4

SymCryptFdef369MaskedCopyAsmLoop:
        mov     Q0, [Q1]
        mov     Q5, [Q2]
        xor     Q0, Q5
        and     Q0, Q4
        xor     Q0, Q5
        mov     [Q2], Q0

        mov     Q0, [Q1 + 8]
        mov     Q5, [Q2 + 8]
        xor     Q0, Q5
        and     Q0, Q4
        xor     Q0, Q5
        mov     [Q2 + 8], Q0

        mov     Q0, [Q1 + 16]
        mov     Q5, [Q2 + 16]
        xor     Q0, Q5
        and     Q0, Q4
        xor     Q0, Q5
        mov     [Q2 + 16], Q0

        // Move on to the next digit

        add     Q1, 24
        add     Q2, 24
        dec     D3
        jnz     SymCryptFdef369MaskedCopyAsmLoop

FUNCTION_END(SymCryptFdef369MaskedCopyAsm)

// VOID
// SYMCRYPT_CALL
// SymCryptFdef369RawMulAsm(
//     _In_reads_(nDigits1*SYMCRYPT_FDEF_DIGIT_NUINT32)                PCUINT32    pSrc1,
//                                                                     UINT32      nDigits1,
//     _In_reads_(nDigits2*SYMCRYPT_FDEF_DIGIT_NUINT32)                PCUINT32    pSrc2,
//                                                                     UINT32      nDigits2,
//     _Out_writes_((nDigits1+nDigits2)*SYMCRYPT_FDEF_DIGIT_NUINT32)   PUINT32     pDst )

MUL_FUNCTION_START(SymCryptFdef369RawMulAsm, 5, 11)

        // Basic structure:
        //   for each word in Src1:
        //       Dst += Src2 * word
        // Register assignments
        //
        // Q0 = tmp for mul
        // QH = tmp for mul
        // Q1 = pSrc1  (updated in outer loop)
        // D2 = # words left from Src1 to process
        // Q3 = pSrc2
        // Q4 = nDigits2
        // Q5 = pDst (incremented in outer loop)
        // Q6 = inner loop pointer into pSrc2
        // Q7 = inner loop pointer into pDst
        // Q8 = word from Src1 to multiply with
        // Q9 = carry
        // D10 = inner loop counter

        inc     D2
        inc     D4
        lea     D2, [D2 + 2*D2]     // nDigits1 * 3 = # words in Src1 to process

        // Outer loop invariant established: Q1, Q3, D4, Q5

        mov     Q6, Q3              // Q6 = pSrc2
        mov     Q7, Q5              // Q7 = pDst + outer loop ctr
        mov     Q8, [Q1]            // mulword
        xor     Q9, Q9
        mov     D10, D4

        // First inner loop overwrites Dst, which avoids adding the current Dst value

ALIGN(16)

SymCryptFdef369RawMulAsmLoop1:
        mov     Q0, [Q6]
        mul     Q8
        add     Q0, Q9
        adc     QH, 0
        mov     [Q7], Q0
        mov     Q9, QH

        mov     Q0, [Q6 + 8]
        mul     Q8
        add     Q0, Q9
        adc     QH, 0
        mov     [Q7 + 8], Q0
        mov     Q9, QH

        mov     Q0, [Q6 + 16]
        mul     Q8
        add     Q0, Q9
        adc     QH, 0
        mov     [Q7 + 16], Q0
        mov     Q9, QH

        add     Q6, 24
        add     Q7, 24
        dec     D10
        jnz     SymCryptFdef369RawMulAsmLoop1

        mov     [Q7], QH                // write last word, cannot overflow because Dst is at least 2 digits long

        dec     D2

ALIGN(16)

SymCryptFdef369RawMulAsmLoopOuter:

        add     Q1, 8                   // move to next word of pSrc1
        add     Q5, 8                   // move Dst pointer one word over
        mov     Q8, [Q1]
        mov     Q6, Q3
        mov     Q7, Q5
        xor     Q9, Q9
        mov     D10, D4

ALIGN(16)

SymCryptFdef369RawMulAsmLoop2:
        mov     Q0, [Q6]
        mul     Q8
        add     Q0, [Q7]
        adc     QH, 0
        add     Q0, Q9
        adc     QH, 0
        mov     [Q7], Q0
        mov     Q9, QH

        mov     Q0, [Q6 + 8]
        mul     Q8
        add     Q0, [Q7 + 8]
        adc     QH, 0
        add     Q0, Q9
        adc     QH, 0
        mov     [Q7 + 8], Q0
        mov     Q9, QH

        mov     Q0, [Q6 + 16]
        mul     Q8
        add     Q0, [Q7 + 16]
        adc     QH, 0
        add     Q0, Q9
        adc     QH, 0
        mov     [Q7 + 16], Q0
        mov     Q9, QH

        add     Q6, 24
        add     Q7, 24
        dec     D10
        jnz     SymCryptFdef369RawMulAsmLoop2

        mov     [Q7], QH           // write next word. (stays within Dst buffer)

        dec     D2
        jnz     SymCryptFdef369RawMulAsmLoopOuter

MUL_FUNCTION_END(SymCryptFdef369RawMulAsm)

// VOID
// SYMCRYPT_CALL
// SymCryptFdef369MontgomeryReduceAsm(
//     _In_                            PCSYMCRYPT_MODULUS      pmMod,
//     _Inout_                         PUINT32                 pSrc,
//     _Out_                           PUINT32                 pDst )

MUL_FUNCTION_START(SymCryptFdef369MontgomeryReduceAsm, 3, 13)

        mov     D4, [Q1 + SymCryptModulusNdigitsOffsetAmd64]                   // nDigits
        inc     D4
        mov     Q5, [Q1 + SymCryptModulusMontgomeryInv64OffsetAmd64]          // inv64

        lea     Q1, [Q1 + SymCryptModulusValueOffsetAmd64]                     // modulus value

        lea     D12, [D4 + 2*D4]  // outer loop counter, in words

        xor     D8, D8

        // General register allocations
        // Q0 = multiply result
        // QH = multiply result
        // Q1 = pointer to modulus value
        // Q2 = pSrc (updated in outer loop)
        // Q3 = pDst
        // D4 = nDigits
        // Q5 = pmMod->tm.montgomery.inv64
        // Q6 = multiplier in inner loop
        // Q7 = carry
        // Q8 = carry out from last word of previous loop iteration
        // Q9 = running pointer in Src
        // Q10 = running pointer in Mod
        // D11 = loop counter
        // D12 = outer loop counter (words)

ALIGN(16)

SymCryptFdef369MontgomeryReduceAsmOuterLoop:

        // start decoder with a few simple instructions, including at least one that requires
        // a uop execution and is on the critical path

        mov     Q6, [Q2]                      // fetch word of Src we want to set to zero
        mov     Q10, Q2
        mov     Q9, Q1

        imul    Q6, Q5                        // lower word is same for signed & unsigned multiply

        mov     D11, D4
        xor     D7, D7

ALIGN(16)

SymCryptFdef369MontgomeryReduceAsmInnerloop:
        // Q0 = mul scratch
        // QH = mul scratch
        // Q1 = pointer to modulus value
        // Q6 = multiplier
        // Q7 = carry (64 bits)
        // Q9  = running ptr to modulus
        // Q10 = running ptr to input/scratch
        // D11 = inner loop counter (digits)
        // D12 = outer loop counter (words)

        mov     Q0, [Q9]
        mul     Q6
        add     Q0, [Q10]
        adc     QH, 0
        add     Q0, Q7
        adc     QH, 0
        mov     [Q10], Q0
        mov     Q7, QH

        mov     Q0, [Q9 + 8]
        mul     Q6
        add     Q0, [Q10 + 8]
        adc     QH, 0
        add     Q0, Q7
        adc     QH, 0
        mov     [Q10 + 8], Q0
        mov     Q7, QH

        mov     Q0, [Q9 + 16]
        mul     Q6
        add     Q0, [Q10 + 16]
        adc     QH, 0
        add     Q0, Q7
        adc     QH, 0
        mov     [Q10 + 16], Q0
        mov     Q7, QH

        add     Q9, 24
        add     Q10, 24
        dec     D11
        jnz     SymCryptFdef369MontgomeryReduceAsmInnerloop

        add     Q7, Q8
        mov     D8, 0
        adc     Q8, 0
        add     Q7, [Q10]
        adc     Q8, 0
        mov     [Q10], Q7

        add     Q2, 8

        dec     D12
        jnz     SymCryptFdef369MontgomeryReduceAsmOuterLoop

        //
        // Most of the work is done - now all that is left is subtract the modulus if it is smaller than the result
        //

        // First we compute the pSrc result minus the modulus into the destination
        mov     D11, D4         // loop ctr
        mov     Q10, Q2         // pSrc
        mov     Q9, Q1          // pMod
        mov     Q7, Q3          // pDst

        // Cy = 0 because the last 'adc Q8,0' resulted in 0, 1, or 2

ALIGN(16)

SymCryptFdef369MontgomeryReduceAsmSubLoop:
        mov     Q0,[Q10]
        sbb     Q0,[Q9]
        mov     [Q7], Q0

        mov     Q0,[Q10 + 8]
        sbb     Q0,[Q9 + 8]
        mov     [Q7 + 8], Q0

        mov     Q0,[Q10 + 16]
        sbb     Q0,[Q9 + 16]
        mov     [Q7 + 16], Q0

        lea     Q10,[Q10 + 24]
        lea     Q9,[Q9 + 24]
        lea     Q7,[Q7 + 24]

        dec     D11
        jnz     SymCryptFdef369MontgomeryReduceAsmSubLoop

        // Finally a masked copy from pSrc to pDst
        // copy if: Q8 == 0 && Cy = 1
        sbb     Q8, 0              // mask (64 bits)

ALIGN(16)

SymCryptFdef369MontgomeryReduceAsmMaskedCopyLoop:
        mov     Q0, [Q2]
        mov     Q1, [Q3]
        xor     Q0, Q1
        and     Q0, Q8
        xor     Q0, Q1
        mov     [Q3], Q0

        mov     Q0, [Q2 + 8]
        mov     Q1, [Q3 + 8]
        xor     Q0, Q1
        and     Q0, Q8
        xor     Q0, Q1
        mov     [Q3 + 8], Q0

        mov     Q0, [Q2 + 16]
        mov     Q1, [Q3 + 16]
        xor     Q0, Q1
        and     Q0, Q8
        xor     Q0, Q1
        mov     [Q3 + 16], Q0

        // Move on to the next digit

        add     Q2, 24
        add     Q3, 24
        dec     D4
        jnz     SymCryptFdef369MontgomeryReduceAsmMaskedCopyLoop

MUL_FUNCTION_END(SymCryptFdef369MontgomeryReduceAsm)

FILE_END()
